plugins {
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlin.serialization)
    id("com.android.library")
}

android {
    defaultConfig {
        externalNativeBuild {
            cmake {
                cppFlags += listOf("-std=c++17")
            }
        }

        ndk {
            abiFilters += listOf("arm64-v8a", "x86_64")
        }
    }

    externalNativeBuild {
        cmake {
            path = file("src/cpp/native_exception_handler/CMakeLists.txt")
            version = "3.22.1"
        }
    }
}

kotlin {
    androidTarget()
    jvm("jvm")
    js(IR) {
        browser {
            binaries.library()
        }
        nodejs {
            binaries.library()
        }
    }
    mingwX64()
    linuxX64()
    macosX64()
    macosArm64()
    iosArm64()
    iosX64()
    iosSimulatorArm64()

    sourceSets {
        val commonMain by getting {
            dependencies {
                // Ktor
                implementation(libs.ktor.client.core)
                implementation(libs.ktor.client.content)
                implementation(libs.ktor.serialization.kotlinx.json)
                // standard
                implementation(libs.kotlinx.datetime)
                implementation(libs.atomicfu)
                implementation(libs.kotlinx.coroutines.core)
                implementation(kotlin("stdlib-common"))
            }
            kotlin.srcDir("build/generated/src/commonMain/kotlin")
        }

        val jniMain by creating {
            dependencies {}
            dependsOn(commonMain)
        }

        val posixMain by creating {
            dependencies {}
            dependsOn(commonMain)
        }

        val androidMain by getting {
            dependencies {
                implementation(libs.ktor.client.okhttp)
            }
            dependsOn(jniMain)
        }

        val jvmMain by getting {
            dependencies {
                implementation(libs.ktor.client.java)
            }
            dependsOn(jniMain)
        }

        val jsMain by getting {
            dependencies {
                implementation(libs.ktor.client.js)
            }
            dependsOn(commonMain)
        }

        val nativeMain by creating {
            dependencies {
                implementation(libs.ktor.client.curl)
            }
            dependsOn(posixMain)
        }
        val mingwX64Main by getting {
            dependsOn(nativeMain)
        }
        val linuxX64Main by getting {
            dependsOn(nativeMain)
        }
        val macosX64Main by getting {
            dependsOn(nativeMain)
        }
        val macosArm64Main by getting {
            dependsOn(nativeMain)
        }

        val iosMain by creating {
            dependencies {
                implementation(libs.ktor.client.darwin)
            }
            dependsOn(posixMain)
        }
        val iosX64Main by getting {
            dependsOn(iosMain)
        }
        val iosArm64Main by getting {
            dependsOn(iosMain)
        }
        val iosSimulatorArm64Main by getting {
            dependsOn(iosMain)
        }
    }
}

android {
    compileSdk = 36
    namespace = "com.cws.print"

    defaultConfig {
        minSdk = 26
        targetSdk = 36
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

val jniBuildDir = file("$buildDir/jni")

fun cmakeTask(project: String, platform: String, generator: String) = tasks.register("buildJni_$platform") {
    group = "jni"
    doLast {
        println("Running cmakeTask for platform:$platform project:$project")

        val outDir = file("$jniBuildDir/$platform")
        outDir.mkdirs()

        val javaHome = System.getenv("JAVA_HOME") ?: "/usr/lib/jvm/java-21-openjdk-amd64"

        val jniPlatformInclude = when(platform) {
            "linux-x86_64" -> "linux"
            "windows-x86_64" -> "win32"
            "macos-x86_64" -> "darwin"
            else -> throw GradleException("Unknown platform")
        }

        val jniIncludeArgs = listOf(
            "-DCMAKE_BUILD_TYPE=Release",
            "-DJAVA_HOME=$javaHome",
            "-DCMAKE_INCLUDE_PATH=$javaHome/include;$javaHome/include/$jniPlatformInclude"
        )

        exec {
            workingDir = outDir
            environment("JAVA_HOME", javaHome)
            println("Running cmake -G $generator")
            commandLine("cmake", "-G", generator, *jniIncludeArgs.toTypedArray(), "../../../src/cpp/$project")
        }

        exec {
            workingDir = outDir
            environment("JAVA_HOME", javaHome)
            println("Running cmake --build .")
            commandLine("cmake", "--build", ".")
        }

        val libName = when(platform) {
            "linux-x86_64" -> "lib$project.so"
            "windows-x86_64" -> "$project.dll"
            "macos-x86_64" -> "lib$project.dylib"
            else -> throw GradleException("Unknown platform")
        }

        copy {
            val fromDir = "$outDir/$libName"
            val toDir = "src/desktopMain/resources/jni/$platform"
            println("Copying $fromDir -> $toDir")
            from(fromDir)
            into(toDir)
        }
    }
}

// Determine platform once during configuration
val osName = System.getProperty("os.name").lowercase()
val osArch = System.getProperty("os.arch").lowercase()

println("OS: $osName, Arch: $osArch")

val generator = when {
    osName.contains("windows") -> "Visual Studio 17 2022"
    osName.contains("linux") -> "Unix Makefiles"
    osName.contains("mac") -> "Unix Makefiles"
    else -> throw GradleException("Unsupported OS: $osName")
}

val platform = when {
    osName.contains("windows") && osArch.contains("64") -> "windows-x86_64"
    osName.contains("linux") && osArch.contains("64") -> "linux-x86_64"
    osName.contains("mac") && osArch.contains("64") -> "macos-x86_64"
    else -> throw GradleException("Unsupported OS/Arch: $osName / $osArch")
}

val cmakeBuild = cmakeTask("native_exception_handler", platform, generator)

tasks.register("buildJni") {
    dependsOn(cmakeBuild)
}